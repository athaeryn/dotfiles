# Source things only if they exist
function try_source () {
  if [ -f "$@" ]; then
    source "$@"
  fi
}


autoload -U colors zsh-mime-setup select-word-style
colors                  # colors
zsh-mime-setup          # run everything as if it's an executable
select-word-style bash  # ctrl+w on words

# Bind some keys
bindkey -v # Vim mode
bindkey '^a' beginning-of-line
bindkey '^d' delete-char
bindkey '^e' end-of-line
bindkey '^r' history-incremental-search-backward
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word

# http://dougblack.io/words/zsh-vi-mode.html
function zle-line-init zle-keymap-select {
    VIM_PROMPT="%{$fg_bold[yellow]%} [% NORMAL]%  %{$reset_color%}"
    RPS1="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/}"
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select
export KEYTIMEOUT=1


# 256 color stuff
try_source ~/.zsh/spectrum.zsh


# Completion
autoload -U compinit
compinit
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'   # ignore case


# Enable using comments interactively (like Bash)
setopt interactivecomments


# Function
function wordplay () {
  while read word; do
  grep "$word" /usr/share/dict/words;
  echo "";
  done
}


function set_prompt {
  function collapsed_pwd {
    echo $(pwd | sed -e "s,^$HOME,~,")
  }
  function git_branch () {
    git branch >/dev/null 2>/dev/null && \
    git branch | grep \* | awk '{printf "|%s| ", $2}'
  }
  setopt PROMPT_SUBST
  PROMPT='%{$fg[green]%}%n%{$reset_color%} $(git_branch)%{$fg[blue]%}$(collapsed_pwd)
%{$reset_color%}%% '
}
set_prompt


path=(
  ~/bin
  ~/golang/bin
  /usr/local/bin
  /usr/local/sbin
  /usr/local/opt/node
  /usr/local/share/npm/bin
  $path
)

export VISUAL=vim
export EDITOR=vim

export NODE_PATH=/usr/local/lib/node
export TERM=xterm-256color
export LSCOLORS=exfxcxdxbxegedabagacad

export GOPATH=~/golang

# Aliases

# Show todos
alias todos="ag TODO\:"

# Get my IP
alias my_ip="ifconfig | grep \"inet \" | grep -v 127.0.0.1 | cut -d\  -f2"

# Use the best vim available.
if which nvim >/dev/null ; then
  alias vim="nvim"
elif which mvim >/dev/null ; then
  alias vim="mvim -v"
fi

# Always use LSCOLORS
alias ls="ls -G"

# ls -la with trailing / for directories
alias lsa="ls -lahp"

# I don't wanna type bundle exec no more
alias be="bundle exec"

# Run a little server
alias serv="python -m SimpleHTTPServer"


export NVM_DIR="/Users/mike/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"  # This loads nvm
